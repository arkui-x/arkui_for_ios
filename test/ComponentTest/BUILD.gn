# Copyright (c) 2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//foundation/arkui/ace_engine/ace_config.gni")

config("component_test_config") {
  configs = [ "$ace_root:ace_config" ]

  include_dirs = [
    "//third_party/",
    "//third_party/flutter",
    "//third_party/flutter/engine",
    "//third_party/flutter/skia",
    "//third_party/flutter/engine/flutter/shell/platform/darwin/common/framework/Headers",
    "//third_party/flutter/engine/flutter/shell/platform/darwin/ios/framework/Headers",
    "//third_party/flutter/engine/flutter/shell/common",
    "$ace_root/frameworks",
    "$ace_root",
    "$ace_root/adapter/ios/entrance/resource",
    "$ace_root/adapter/ios/capability/clipboard",
    "$ace_root/adapter/ios/capability/environment",
    "$ace_root/adapter/ios/capability/storage",
    "$ace_root/adapter/ios/capability/vibrator",
    "$ace_root/adapter/ios/capability/texture",
    "$ace_root/adapter/ios/capability/video",
    "$ace_root/adapter/ios/capability/camera",
    "$ace_root/adapter/ios/test/ComponentTest",
  ]

  cflags_objc = [ "-fvisibility=default" ]

  cflags_objcc = cflags_objc
}

# build static
template("ace_component_test_ios_source_set") {
  forward_variables_from(invoker, "*")

  ohos_source_set(target_name) {
    part_name = ace_engine_part
    defines += invoker.defines

    configs = [
      "$ace_root:ace_config",
      ":component_test_config",
    ]

    cflags_objc = [
      "-fvisibility=default",
      "-fobjc-arc",
      "-fobjc-weak",
    ]

    cflags_objcc = cflags_objc

    sources = [
      "$ace_root/adapter/ios/test/ComponentTest/ace_clipboard_test.mm",
      "$ace_root/adapter/ios/test/ComponentTest/ace_environment_test.mm",
      "$ace_root/adapter/ios/test/ComponentTest/ace_init_task_excutor.mm",
      "$ace_root/adapter/ios/test/ComponentTest/ace_storage_test.mm",
      "$ace_root/adapter/ios/test/ComponentTest/ace_vibrator_test.mm",
    ]

    deps = [ "$ace_root/adapter/ios/capability:ace_capability_ios" ]

    part_name = "ace_engine_cross"
    subsystem_name = "arkui"
  }
}

foreach(item, ace_platforms) {
  platform = item.name
  if (platform == "ios") {
    ace_component_test_ios_source_set("ace_component_test_ios") {
      defines = []
      config = {
      }

      if (defined(item.config)) {
        config = item.config
      }
      if (defined(config.defines)) {
        defines = config.defines
      }
    }
  }
}
