# Copyright (c) 2023-2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//foundation/arkui/ace_engine/ace_config.gni")
import("//build_plugins/version.gni")

# build static
template("stage_ios_standard_source_set") {
  forward_variables_from(invoker, "*")
  ohos_source_set(target_name) {
    defines = []
    defines += invoker.defines
    cflags_cc += invoker.cflags_cc

    configs = [
      "$ace_root:ace_config",
      "//foundation/appframework/ability/ability_runtime/cross_platform/interfaces/kits/native/appkit:appkit_native_config",
      "//third_party/json:nlohmann_json_config",
    ]

    include_dirs = [
      "./",
      "//foundation/arkui/ace_engine/adapter/ios/entrance",
      "//foundation/appframework/ability/ability_runtime/cross_platform/interfaces/kits/native/appkit",
      "//foundation/arkui/ace_engine/adapter/ios/capability/bridge",
      "//foundation/arkui/ace_engine/adapter/ios/capability/web",
      "//foundation/arkui/ace_engine/adapter/ios/entrance/accessibility",
      "$ace_root/frameworks",
      "$ace_root",
      "//foundation/arkui/ace_engine/adapter/ios/entrance/plugin_lifecycle",
      "$ace_root/adapter/ios/capability/platformview",
      "//foundation/arkui/ace_engine/adapter/ios/entrance/logIntercept",
    ]

    if (target_cpu == "arm64") {
      defines += [ "_ARM64_" ]
    }

    defines += [
      "BUILD_FOR_ARKUI",
      "ARKUI_X_VERSION=\"$arkuix_sdk_version\"",
    ]

    cflags_objc = [
      "-fvisibility=default",
      "-fobjc-arc",
      "-fobjc-weak",
      "-Wno-nullability-completeness",
      "-O0",
    ]

    cflags_objcc = cflags_objc

    sources = [
      "InstanceIdGenerator.mm",
      "StageApplication.mm",
      "StageAssetManager.mm",
      "StageConfigurationManager.mm",
      "StageContainerView.mm",
      "StageSecureContainerView.mm",
      "StageViewController.mm",
      "ability_context_adapter.mm",
      "application_context_adapter.mm",
      "stage_application_info_adapter.cpp",
      "stage_asset_provider.mm",
      "version_printer.cpp",
      "window_view_adapter.mm",
    ]
    deps = [
      "//foundation/appframework/ability/ability_runtime/cross_platform/interfaces/kits/native/appkit:cross_platform_appkit_native",
      "//foundation/arkui/ace_engine/adapter/ios/entrance:ace_ios_entrance",
      "//foundation/arkui/ace_engine/adapter/ios/entrance/logIntercept:logIntercept",
      "//foundation/arkui/ace_engine/adapter/ios/entrance/plugin_lifecycle:plugin_lifecycle",
    ]

    frameworks = [
      "CoreFoundation.framework",
      "Foundation.framework",
      "UIKit.framework",
      "PhotosUI.framework",
      "UniformTypeIdentifiers.framework",
      "Photos.framework",
      "MobileCoreServices.framework",
    ]

    subsystem_name = "arkui"
  }
}

foreach(item, ace_platforms) {
  platform = item.name
  if (platform == "ios") {
    stage_ios_standard_source_set("stage_ios_entrance") {
      defines = []
      cflags_cc = []
      config = {
      }

      if (defined(item.config)) {
        config = item.config
      }

      if (defined(config.defines)) {
        defines = config.defines
      }

      if (defined(config.cflags_cc)) {
        cflags_cc = config.cflags_cc
      }
    }
  }
}
