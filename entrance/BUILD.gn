# Copyright (c) 2022-2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//foundation/arkui/ace_engine/ace_config.gni")

# build static
template("ace_ios_standard_source_set") {
  forward_variables_from(invoker, "*")

  ohos_source_set(target_name) {
    defines += invoker.defines
    cflags_cc += invoker.cflags_cc

    include_dirs = [
      "$ace_root/frameworks",
      "$ace_root",
      "$ace_root/adapter/ios/entrance",
      "$ace_root/adapter/ios/entrance/resource",
      "$ace_root/adapter/ios/entrance/accessibility",
      "$ace_root/adapter/ios/capability/bridge",
      "$ace_root/adapter/ios/capability/texture",
      "$ace_root/adapter/ios/capability/video",
      "$ace_root/adapter/ios/capability/platformview",
      "$ace_root/adapter/ios/capability/surface",
      "$ace_root/adapter/ios/capability/web",
      "//foundation/arkui/ace_engine/adapter/ios/stage/ability",
      "//foundation/appframework/window_manager/interfaces/innerkits/wm",
      "//foundation/appframework/window_manager/interfaces/innerkits/dm",
      "//foundation/appframework/window_manager/dm/include",
      "//foundation/appframework/window_manager/utils/include",
      "//foundation/graphic/graphic_surface/interfaces/inner_api/surface",
      "//base/hiviewdfx/hilog/interfaces/native/innerkits/include",
    ]

    sources = [
      "AcePlatformPlugin.mm",
      "AceSurfaceHolder.mm",
      "AceTextureHolder.mm",
      "DownloadManager.mm",
      "UINavigationController+StatusBar.mm",
      "WantParams.mm",
      "WindowView.mm",
      "ace_application_info_impl.cpp",
      "ace_bridge.mm",
      "ace_download_bridge.mm",
      "ace_platform_plugin.cpp",
      "ace_pointer_data.cc",
      "ace_pointer_data_packet.cc",
      "ace_resource_register.cpp",
      "ace_translate_manager.cpp",
      "capability_registry.cpp",
      "display_info.mm",
      "display_manager_agent.cpp",
      "download_manager.cpp",
      "dump_helper.cpp",
      "foldable_window_ios.cpp",
      "html/html_to_span.cpp",
      "html/span_to_html.cpp",
      "interaction/interaction_impl.cpp",
      "mmi_event_convertor.mm",
      "picker/picker_haptic_factory.cpp",
      "report/reporter_impl.cpp",
      "touch_event.cpp",
      "touch_event_convertor.cpp",
      "udmf/udmf_impl.cpp",
      "ui_session/ui_session_json_util.cpp",
      "ui_session/ui_session_manager_ios.cpp",
      "utils.cpp",
      "vibrator/vibrator_utils.cpp",
      "virtual_rs_window.mm",
      "xcollie/xcollieInterface_impl.cpp",
    ]

    if (target_cpu == "arm64") {
      defines += [ "_ARM64_" ]
    }

    defines += [
      "BUILD_FOR_ARKUI",
      "CROSS_PLATFORM",
    ]

    configs = [
      "$ace_root:ace_config",
      "//foundation/appframework/ability/ability_runtime/cross_platform/interfaces/kits/native/ability:abilitykit_native_config",
      "//foundation/appframework/ability/ability_runtime/cross_platform/interfaces/kits/native/appkit:appkit_native_config",
      "//foundation/appframework/ability/ability_runtime/cross_platform/interfaces/inner_api/hilog:ability_cross_platform_hilog_config",
      "//foundation/appframework/ability/ability_runtime/cross_platform/interfaces/inner_api/jsruntime:jsruntime_config",
    ]

    cflags_objc = [
      "-fvisibility=default",
      "-fobjc-weak",
    ]

    cflags_objcc = cflags_objc

    deps = [
      "$ace_root/adapter/ios/capability:ace_capability_ios",
      "$ace_root/adapter/ios/entrance/accessibility:accessibility",
      "$ace_root/adapter/ios/entrance/resource:resource",
      "$ace_root/frameworks/core/components/theme:build_theme_code",
      "//foundation/distributeddatamgr/udmf/adapter:arkui_x_udmf_data_napi",
      "//foundation/graphic/graphic_2d/rosen/modules/render_service_client:librender_service_client_static",
      "//third_party/zlib:libz",
      "//third_party/libxml2:static_libxml2",
    ]

    frameworks = [
      "UIKit.framework",
      "OpenGLES.framework",
      "QuartzCore.framework",
    ]

    include_dirs += [
      "//base/global/resource_management/interfaces/inner_api/include",
      "//foundation/distributeddatamgr/udmf/interfaces/jskits/data",
    ]

    if (defined(config.enable_rosen_backend) && config.enable_rosen_backend) {
      deps += [ "//foundation/graphic/graphic_2d/rosen/modules/render_service_client:librender_service_client_static" ]
      deps += [ "//foundation/arkui/napi:ace_napi" ]
    }

    #add multimodalinput
    deps += [ "//foundation/multimodalinput/input/frameworks/proxy:libmmi-client-crossplatform" ]

    #add drag
    if (defined(config.enable_drag_framework) && config.enable_drag_framework) {
      defines += [ "OHOS_BUILD_ENABLE_ARKUI_X" ]
      deps += [ "//base/msdp/device_status/services/interaction/drag:interaction_client_crossplatform" ]
    }
  }
}

foreach(item, ace_platforms) {
  platform = item.name
  if (platform == "ios") {
    ace_ios_standard_source_set("ace_ios_entrance") {
      defines = []
      cflags_cc = []
      config = {
      }

      if (defined(item.config)) {
        config = item.config
      }

      if (defined(config.defines)) {
        defines = config.defines
      }

      if (defined(config.cflags_cc)) {
        cflags_cc = config.cflags_cc
      }
    }
  }
}
