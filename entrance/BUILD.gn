# Copyright (c) 2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//foundation/arkui/ace_engine/ace_config.gni")

# build static
template("ace_ios_standard_source_set") {
  forward_variables_from(invoker, "*")

  ohos_source_set(target_name) {
    defines += invoker.defines
    cflags_cc += invoker.cflags_cc

    include_dirs = [
      "//third_party/",
      "$ace_root/frameworks",
      "$ace_root",
      "$ace_root/adapter/ios/entrance/resource",
      "$ace_root/adapter/ios/capability/texture",
      "$ace_root/adapter/ios/capability/video",
      "$ace_root/adapter/ios/capability/camera",
    ]

    sources = [
      "ace_application_info_impl.cpp",
      "ace_bridge.mm",
      "ace_container.cpp",
      "ace_resource_register.cpp",
      "capability_registry.cpp",
      "flutter_ace_view.cpp",
    ]

    if (target_cpu == "arm64") {
      defines += [ "_ARM64_" ]
    }

    defines += [
      "BUILD_FOR_ARKUI"
    ]

    configs = [ "$ace_root:ace_config" ]

    cflags_objc = [ "-fvisibility=default" ]

    cflags_objcc = cflags_objc

    deps = [
      "$ace_root/adapter/ios/capability:ace_capability_ios",
      "$ace_root/adapter/ios/entrance/resource:resource",
      "$ace_root/frameworks/core/components/theme:build_theme_code",
    ]

    if(enable_ng_build){
      include_dirs += [
        "//third_party/flutter_ace_shell",
        "//third_party/flutter_ace_shell/engine",
        "//third_party/flutter_ace_shell/third_party/skia",
        "//third_party/flutter_ace_shell/engine/flutter/third_party/txt/src"
      ]

      sources += [ "AceViewControllerNG.mm"]

      deps += [
          "$ace_flutter_engine_root_new:third_party_flutter_engine_dev_$platform",
          "$ace_flutter_engine_root_new/skia:ace_skia_dev_$platform",
      ]
    }else{

      include_dirs += [
        "//third_party/flutter",
        "//third_party/flutter/engine",
        "//third_party/flutter/third_party/skia",
        "//third_party/flutter/engine/flutter/shell/platform/darwin/common/framework/Headers",
        "//third_party/flutter/engine/flutter/shell/platform/darwin/ios/framework/Headers",
        "//third_party/flutter/engine/flutter/shell/common",
      ]

      sources += [ "AceViewController.mm" ]

      deps += [
          "$ace_flutter_engine_root:third_party_flutter_engine_$platform",
          "$ace_flutter_engine_root/third_party/skia:ace_skia_$platform",
      ]
    }
  }
}

foreach(item, ace_platforms) {
  platform = item.name
  if (platform == "ios") {
    ace_ios_standard_source_set("ace_ios_entrance") {
      platform = item.name
      defines = []
      cflags_cc = []
      config = {
      }

      if (defined(item.config)) {
        config = item.config
      }

      if (defined(config.defines)) {
        defines = config.defines
      }

      if (defined(config.cflags_cc)) {
        cflags_cc = config.cflags_cc
      }
    }
  }
}
